#
# Copyright 2008-2017 Katherine Flavel
#
# See LICENCE for the full copyright terms.
#

/[\r\n\t ]+/;
"#" .. "\n";

'"' .. '"' -> $label {
	'\\' -> $esc;
	'\"' -> $esc;
	'\f' -> $esc;
	'\n' -> $esc;
	'\r' -> $esc;
	'\t' -> $esc;
	'\v' -> $esc;

	'\'  /[0-7]+/     -> $oct;
	'\x' /[0-9a-f]+/i -> $hex;

	/./ - ($esc | $oct | $hex) -> $char;
}

"'" .. "'" -> $label {
	/./ -> $char;
}

'start:' -> $start;
'end:'   -> $end;

/[a-z0-9_]+/i -> $ident;

'=' -> $equals;
'[' -> $openendids .. ']' -> $closeendids {
	/[\r\n\t ]+/;
	"#" .. "\n";

	','  -> $comma;

	# using a lexical zone because $endid overlaps $ident
	/[0-9]+/ -> $endid;
}

'->' -> $to;
'?'  -> $any;
';'  -> $sep;
','  -> $comma;

