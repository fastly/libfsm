#
# Copyright 2008-2017 Katherine Flavel
#
# See LICENCE for the full copyright terms.
#

'(?:'  -> $opensub;
'('  -> $opencapture;
')'  -> $close;

# predicates
'^'  -> $start;
'$'  -> $end;

# operators
'?'  -> $opt;
'*'  -> $star;
'+'  -> $plus;
'.'  -> $any;
'|'  -> $alt;

# standard escapes
'\\' -> $esc;
'\^' -> $esc;
'\$' -> $esc;
'\?' -> $esc;
'\*' -> $esc;
'\+' -> $esc;
'\.' -> $esc;
'\|' -> $esc;
'\(' -> $esc; '\)' -> $esc;
'\{' -> $esc; # note no \}
'\[' -> $esc; # note no \]

# whitespace escapes
'\f' -> $esc;
'\n' -> $esc;
'\r' -> $esc;
'\t' -> $esc;

# perl/unix-ish escapes
'\a' -> $esc;
'\c' /./ -> $control;

# character type escapes
# Note, this needs a double underscore because sid expects any '_'
# in a C identifier converts to "__".
'\d' -> $named__char__class;
'\D' -> $named__char__class;
'\h' -> $named__char__class;
'\H' -> $named__char__class;
'\s' -> $named__char__class;
'\S' -> $named__char__class;
'\v' -> $named__char__class;
'\V' -> $named__char__class;
'\w' -> $named__char__class;
'\W' -> $named__char__class;
# This break the uppercase/lowercase symmetry; conceptually, it's the
# negation of `.`, which matches everything except `\n`.
'\N' -> $named__char__class;

'\'   /[0-7]{1,3}/     -> $oct;
'\o{' /[0-7]+/i '}'    -> $oct;
'\x'  /[0-9a-f]{1,2}/i -> $hex;
'\x{' /[0-9a-f]+/i '}' -> $hex;

'\' /./ - ($esc | $control | $oct | $hex | '\Q' | $named__char__class) -> $noesc;
'\' -> $invalid;

'(?' -> $openflags .. ')' -> $closeflags {
	'-' -> $negate;
	'i' -> $flag__insensitive;
	/[a-z]/ - $flag__insensitive -> $flag__unknown;
}

'[' -> $opengroup .. ']' -> $closegroup {
	'^' -> $invert;
	'-' -> $range;

	# extension escapes
	'\\' -> $esc;
	'\^' -> $esc;
	'\-' -> $esc;

	# whitespace escapes
	'\f' -> $esc;
	'\n' -> $esc;
	'\r' -> $esc;
	'\t' -> $esc;

	# perl/unix-ish escapes
	'\a' -> $esc;
	'\c' /./ -> $control;

	# other metacharacters that can be escaped within square brackets
	# for consistency's sake, although it isn't strictly necessary
	'\$' -> $esc;
	'\.' -> $esc;
	'\[' -> $esc;
	'\|' -> $esc;
	'\(' -> $esc;
	'\?' -> $esc;
	'\*' -> $esc;
	'\+' -> $esc;
	'\{' -> $esc;

	'\d' -> $named__char__class;
	'\D' -> $named__char__class;
	'\h' -> $named__char__class;
	'\H' -> $named__char__class;
	'\s' -> $named__char__class;
	'\S' -> $named__char__class;
	'\v' -> $named__char__class;
	'\V' -> $named__char__class;
	'\w' -> $named__char__class;
	'\W' -> $named__char__class;
	'\N' -> $named__char__class;

	'\'   /[0-7]{1,3}/     -> $oct;
	'\o{' /[0-7]+/i '}'    -> $oct;
	'\x'  /[0-9a-f]{1,2}/i -> $hex;
	'\x{' /[0-9a-f]+/i '}' -> $hex;

	'\' /./ - ($esc | $control | $oct | $hex | '\Q' | $named__char__class) -> $noesc;
	'\' -> $invalid;

	# FIXME: negated character classes, e.g. [:^alpha:]
	'[:alnum:]'  -> $named__char__class;
	'[:alpha:]'  -> $named__char__class;
	'[:ascii:]'  -> $named__char__class;
	'[:blank:]'  -> $named__char__class;
	'[:cntrl:]'  -> $named__char__class;
	'[:digit:]'  -> $named__char__class;
	'[:graph:]'  -> $named__char__class;
	'[:lower:]'  -> $named__char__class;
	'[:print:]'  -> $named__char__class;
	'[:punct:]'  -> $named__char__class;
	'[:space:]'  -> $named__char__class;
	'[:upper:]'  -> $named__char__class;
	'[:word:]'   -> $named__char__class;
	'[:xdigit:]' -> $named__char__class;

	# XXX: /./ - the above
	/[^\^\-\\]/ -> $char;

	'\Q' .. '\E' {
		/./ -> $char;
	}
}

'{' -> $opencount .. '}' -> $closecount {
	','      -> $sep;
	/[0-9]+/ -> $count;
}

'\Q' .. '\E' {
	/./ -> $char;
}

/./ - ($opensub | $opencapture | $close | $start | $end
	| $opt | $star | $plus | $any | $alt
	| $esc | $oct | $hex | $invalid
	| $opengroup | $opencount) -> $char;

