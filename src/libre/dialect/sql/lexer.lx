#
# Copyright 2008-2017 Katherine Flavel
#
# See LICENCE for the full copyright terms.
#

'_' -> $any;
'%' -> $many;

'('  -> $opensub;
')'  -> $closesub;

# operators
'?'  -> $opt;
'*'  -> $star;
'+'  -> $plus;
'|'  -> $alt;

'[' -> $opengroup .. ']' -> $closegroup {
	'^' -> $invert;
	'-' -> $range;

	'[:ALNUM:]'      -> $named__class;
	'[:ALPHA:]'      -> $named__class;
	'[:DIGIT:]'      -> $named__class;
	'[:LOWER:]'      -> $named__class;
	'[:SPACE:]'      -> $named__class;
	'[:UPPER:]'      -> $named__class;
	'[:WHITESPACE:]' -> $named__class;

	# XXX: /./ - the above
	/[^\^\-]/ -> $char;
}

'{' -> $opencount .. '}' -> $closecount {
     /[0-9]+/ -> $count;
     ',' -> $sep;
}


/./ - ($any | $many
	| $opensub | $closesub
	| $opt | $star | $plus | $alt
	| $opengroup | $closegroup
	| $opencount | $closecount) -> $char;

