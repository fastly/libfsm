#
# Copyright 2008-2017 Katherine Flavel
#
# See LICENCE for the full copyright terms.
#

'_' -> $any;
'%' -> $many;

'('  -> $opensub;
')'  -> $closesub;

# operators
'?'  -> $opt;
'*'  -> $star;
'+'  -> $plus;
'|'  -> $alt;

'[' -> $opengroup .. ']' -> $closegroup {
	'^' -> $invert;
	'-' -> $range;

	'[:ALNUM:]'      -> $named__char__class;
	'[:ALPHA:]'      -> $named__char__class;
	'[:DIGIT:]'      -> $named__char__class;
	'[:LOWER:]'      -> $named__char__class;
	'[:SPACE:]'      -> $named__char__class;
	'[:UPPER:]'      -> $named__char__class;
	'[:WHITESPACE:]' -> $named__char__class;

	# XXX: /./ - the above
	/[^\^\-]/ -> $char;
}

'{' -> $opencount .. '}' -> $closecount {
     /[0-9]+/ -> $count;
     ',' -> $sep;
}


/./ - ($any | $many
	| $opensub | $closesub
	| $opt | $star | $plus | $alt
	| $opengroup | $closegroup
	| $opencount | $closecount) -> $char;

